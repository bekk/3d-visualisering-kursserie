# Perlin noise + Lighting

![Resultat perlin noise](./img/perlin-noise.gif)

I denne oppgaven skal vi bruke Perlin st칮y til 친 lage en procedurally generated verden som vi lyser opp med v친r egen lys-kilde.

## Utdelt oppsett

Den utdelte koden er kjent fra tidligere oppgaver. Du f친r et ferdig oppset for three.js med camera, scene og renderer.

I tillegg f친r du en funksjon som lager et XY-plan vi skal bruke til 친 visualisere ting p친. Planet har et ShaderMaterial med en vertex og en fragment shader. I tillegg er det laget en uniform for tid i sekunder, som vi skal bruke til animasjoner etterhvert.

Du kj칮rer koden med f칮lgende kommando

```sh
npm install
npm run perlin-noise
```

N친r du kj칮rer koden vil du se en hvit firkant midt p친 skjermen, da vet du at alt er OK og du kan starte med oppgaven.

Skulle du bli sittende fast kan du alltids ta en titt p친 fasiten med:

```sh
npm run fasit-perlin-noise
```

## Del 1: Perlin noise og glslify

Vi har eksperimentert med random-verdier tidligere i kurset, og da har du kanskje lagt merke til at det er vanskelig 친 f친 randomness til 친 se bra ut (utover komplett kaos). En av m친tene vi kan beholde b친de random verdier og f친 ting som ser mer sammenhengende ut er Perlin Noise.

Perlin noise er en spesiell type funksjon som genererer pseudo random verdier som henger sammen. Den finnes i b친de 2D, 3D og 4D (egentlig nD). M친ten Perlin noise fungerer p친 er at du gir funksjonen en vektor i `n` dimensjoner, og du vil f친 tilbake en float verdi som representerer det punktet i det `n`-dimensjonale rommet. Det som er kult med Perlin noise er at tallene du f친r til bake for punkter som er n칝rme hverandre ogs친 vil v칝re n칝re hverandre.

Perlin, og arvtageren Simplex, er veldig mye brukt. Noe som er en fordel for oss, siden noen da har implementert algoritmene for `glsl` s친 vi kan bruke dem til 친 lage g칮yale ting.

For 친 benytte oss av en ferdiglaga perlin-funksjon trenger vi 친 bruke et verkt칮y som heter `glslify`. `glslify` prosesserer glsl-filer for oss og syr sammen kode p친 samme m친te som vi gj칮r i andre spr친k.

For 친 ta i bruk `glslify` p친 shaderne v친re trenger vi 친 fortelle `glslify` hvilke filer som skal prosesseres. Det gj칮r vi ved 친 pakke inn shaderne v친re i et kall til `glsl`-funksjonen fra `glslify`-pakka:

`index.js`:
```diff
+const glsl = require("glslify");

-const vertexShaderCode = fs.readFileSync(
+const vertexShaderCode = glsl(fs.readFileSync(
  `${__dirname}/vertexshader.glsl`,
  "utf8"
-);
+));
-const fragmentShaderCode = fs.readFileSync(
+const fragmentShaderCode = glsl(fs.readFileSync(
  `${__dirname}/fragmentshader.glsl`,
  "utf8"
-);
+));
```

Da f친r vi muligheten til 친 gj칮re dette i shaderkoden v친r:

`vertexshader.glsl`:
```diff
+#pragma glslify: snoise3 = require(glsl-noise/simplex/3d);
```

Da henter vi inn `snoise3`-funksjonen fra `glsl-noise`-pakka. `snoise3` er 3D-versjonen av Simplex Noise. N친 kan vi bruke noise til 친 fargelegge pixlene v친re:

`vertexshader.glsl`:
```diff
#pragma glslify: snoise3 = require(glsl-noise/simplex/3d);

uniform float time;

+varying float noise;

void main() {
  vec4 modelSpaceCoordinates = vec4(position, 1.0);
  vec4 worldSpaceCoordinates = modelViewMatrix * modelSpaceCoordinates;
  vec4 screenSpaceCoordinate = projectionMatrix * worldSpaceCoordinates;

+  noise = snoise3(position);

  gl_Position = screenSpaceCoordinate;
}
```

Her lagrer vi noise-verdien som hentes ut fra posisjonen til vertexen og sender den videre til fragmentshaderen som en varying. Sa kan vi hente ut den varying-verdien i fragmentshaderen:

`fragmentshader.glsl`:
```diff
uniform float time;

+varying float noise;

void main() {
-    vec3 color = vec3(1.0, 1.0, 1.0);
+    vec3 color = vec3(noise, noise, noise);
    float alpha = 1.0;

    gl_FragColor = vec4(color, alpha);
}
```

Dette vil gi oss en ganske fin svart-hvit og noisy firkant.

![noisy firkant](./img/noise1.png)

## Del2: Bevegelig og interssant noise

Det som er g칮y med Perlin og Simplex noise er at verdiene er stabile. Du vil alltid f친 samme verdi n친r du kaller funksjonen med samme verdi. Dette kan vi utnytte til 친 f친 det til 친 se ut som ting beveger seg. Ved 친 benytte `time`-verdien v친r sammen med noise-funksjonen kan vi lage noe kult:

`vertexshader.glsl`:
```diff
-  noise = snoise3(position);
+  noise = snoise3(vec3(position.x, position.y + time, position.z));
```

>游눠 Pr칮v 친 legge til eller trekke fra time p친 en av de andre dimensjonene for 친 f친 feltet til 친 bevege seg i ulike retninger. Hvis du legger det til z-komponenten skjer det noe annerledes, hva tror du er 친rsaken til det?

For 친 gj칮re noisen litt mer interessant g친r det an 친 endre litt p친 hvordan vi generer det. Feks ved 친 sample noise i et mindre areal og legge det til et st칮rre areal. Hvis vi skalerer ned posisjons-vektoren vi sender til noise-funksjonen reduserer vi oppl칮seligheten til st칮yen:

`vertexshader.glsl`:
```diff
-  noise = snoise3(vec3(position.x, position.y + time, position.z));
+  noise = snoise3(0.25 * vec3(position.x, position.y + time, position.z));
```

Et anna triks som lager g칮yale resultater er 친 legge sammen to ulike noise-verdier:

`vertexshader.glsl`:
```diff
-  noise = snoise3(0.25 * vec3(position.x, position.y + time, position.z));
+  float n1 = snoise3(0.25 * vec3(position.x, position.y + time, position.z));
+  float n2 = snoise3(0.1 * vec3(position.x, position.y + time, position.z));
+  noise = n1 + n2;
```

Da f친r du noe som begynner 친 minne om et veldig blurry satelittbilde av terreng. Da er vi p친 spor av noe kult.

>游눠 Pr칮v 친 justere verdiene som du ganger med posisjonsvektoren, endre regnestykket til 친 trekke fra noise, eller legge til flere noises.

## Del 3: Generert terreng!

F칮r vi g친r i gang med 친 generere terreng b칮r vi rydde litt i shader-koden v친r. Vi starter med 친 trekke ut noise-genereringen til en egen funksjon:

`vertexshader.glsl`:
```diff
+float noiseFunction(float t, vec3 position) {
+  vec3 offsetPosition = vec3(position.x, position.y + t, position.z);
+  float n1 = snoise3(0.25 * offsetPosition);
+  float n2 = snoise3(0.1 * offsetPosition);
+  float n = n1  + n2;
+
+  return n;
+}

void main() {
  vec4 modelSpaceCoordinates = vec4(position, 1.0);
  vec4 worldSpaceCoordinates = modelViewMatrix * modelSpaceCoordinates;
  vec4 screenSpaceCoordinate = projectionMatrix * worldSpaceCoordinates;

-  float n1 = snoise3(0.25 * vec3(position.x, position.y + time, position.z));
-  float n2 = snoise3(0.1 * vec3(position.x, position.y + time, position.z));
-  noise = n1 + n2;
+  noise = noiseFunction(time, position);

  gl_Position = screenSpaceCoordinate;
}
```

For 친 lage illusjonen om terreng kan vi bruke noise-verdien til 친 flytte vertexene til planet vi jobber med langs z-aksen (den aksen som peker ut fra skjermen):

`vertexshader.glsl`:
```diff
void main() {
+  float n = noiseFunction(time, position);
-  vec4 modelSpaceCoordinates = vec4(position, 1.0);
+  vec4 modelSpaceCoordinates = vec4(position.xy, position.z + n, 1.0);
  vec4 worldSpaceCoordinates = modelViewMatrix * modelSpaceCoordinates;
  vec4 screenSpaceCoordinate = projectionMatrix * worldSpaceCoordinates;

-  noise = noiseFunction(time, position);
+  noise = n;

  gl_Position = screenSpaceCoordinate;
}
```

N친r du ha gjort dette vil du se at omtrent ingenting har forandra seg, men hvis du bruker musa til 친 tilte kameraet litt s친 vil du se at planet har begynt 친 ligne med p친 terreng!

![noisy terreng](./img/noise2.png)

Det som er greia her er at PlaneGeometry i three.js alltid er lagt til XY-planet, hvis vi 칮nsker oss et XZ-plan med Y-aksen oppover s친 m친 vi lage noe eget... eller bruke litt trigonometri til 친 fikse problemet.

`index.js`:
```diff
function init() {
  scene = new THREE.Scene();
+  scene.rotation.set(-Math.PI / 2, 0, 0);

  initCamera();
  initRenderer();
  initDatGui();
  initPlane();

  document.body.appendChild(renderer.domElement);
}
```

Ved 친 rotere hele scenen med -90 grader (90 grader === PI/2) langs X-aksen flytter vi om p친 ting slik at XY-planet er vannrett og Z-aksen peker oppover. Vi har n친 et terreng!

For 친 se terrenget fra en litt mer interessant vinkel kan vi flytte litt p친 utgangsposisjonen til kameraet v친rt:

`index.js`:
```diff
-  camera.position.set(0, 0, 50);
+  camera.position.set(0, 10, 15);
```

N친 ser det nesten ut som vi flyr over et slags sp칮kelseslandskap 游놑

Hvis vi vil, s친 kan vi gj칮re 친sene i landskapet h칮yere ved 친 multiplisere noise-verdien med en eller annen faktor, feks `2.5`:

`vertexshader.glsl`:
```diff
float noiseFunction(float t, vec3 position) {
  vec3 offsetPosition = vec3(position.x, position.y + t, position.z);
  float n1 = snoise3(0.25 * offsetPosition);
  float n2 = snoise3(0.1 * offsetPosition);
  float n = n1  + n2;

-  return n;
+  return n * 2.5;
}
```

## Del 4: Litt farger

Det er kjedelig med ting som er svart-hvitt, la oss legge til litt farge p친 terrenget v친rt. Vi kan benytte oss av noise-verdien til 친 fargelegge ting med en gradient, feks fra m칮rkegr칮nn til lysegr칮nn.

`fragmentshader.glsl`:
```diff
void main() {
-  vec3 color = vec3(noise);
+  vec3 color = mix(vec3(0.0, 0.25, 0.0), vec3(0.0, 1.0, 0.0), noise / 2.5);
  float alpha = 1.0;

  gl_FragColor = vec4(color, alpha);
}
```

For 친 gj칮re dette bruker vi den innebygde `mix`-funksjonen. Den vil interpolere mellom to verdier gitt et tall mellom 0 og 1 (n칝re 0 betyr n칝re den fr칮ste verdien, mens n칝re 1 betyr n칝re den andre verdien). Siden noise-verdien vi f친r ut er ganget med `2.5` kan vi dele p친 `2.5` her, for 친 f친 verdien innafor `[0, 1]` igjen. N친 f친r vi en deilig neon-gr칮nn farge p친 ting.

I tillegg kan vi bruke et triks fra Three.js til 친 gi oss skikkelig matrix-f칮lelse:

`index.js`:
```diff
let material = new THREE.ShaderMaterial({
  vertexShader: vertexShaderCode,
  fragmentShader: fragmentShaderCode,
+  wireframe: true,
  uniforms
});
```

Om du liker effekten kan du la den v칝re p친, det er opp til deg!

## Del 5: Let there be darkness! 游댡

Det er litt kjedelig at hele terrenget synes hele tiden, mye g칮yere hvis du bare f친r se litt av det. For 친 gj칮re det kan vi legge til litt kunstig belysning.

Vi starter med 친 definere hvor lyset v친rt skal befinne seg

`index.js`:
```diff
let uniforms = {
-  time: { value: 0 }
+  time: { value: 0 },
+  light: { value: new THREE.Vector3(0, 0, 2) }
};
```

Vi setter lyset til 친 befinne seg i `[0,0,2]`, det vil si i sentrum av planet v친rt og litt opp (langs Z-aksen). Vi setter lys-posisjonen som en uniform, siden den vil v칝re lik for alle vertices i scena. N친 kan vi bruke lys-posisjonen til 친 belyse scena v친r.

>仇뎂hree.js har noen innebygde lys-greier, men de fungerer ikke n친r vi bruker v친re egne shadere. S친 vi m친 derfor implementere v친r egen belynsing. Men det gj칮r ikke noe, siden det er ganske l칝rerikt.

Den lysmodellen vi skal implementere kalles diffuse lightning. Det er en veldig enkel lysmodell som belyser overflaten til objekter med flatt lys.

<img src="https://upload.wikimedia.org/wikipedia/commons/b/bd/Lambert2.gif" alt="Lambert2.gif" width="194" height="145">

For 친 regne ut hvor belyst noe blir trenger vi 3 ting: normal-vektoren og posiosjonen til vertexen og posisjonen til lyset. Posisjonen til lyset har vi som en uniform, normal-vektoren og vertex-posisjonen kan vi sende med som varying.

`vertexshader.glsl`
```diff
+varying vec3 positionVec;
+varying vec3 normalVec;
varying float noise;

void main() {
  // ...

+  positionVec = position;
+  normalVec = normal;
  noise = n;

  // ...
}
```

`fragmentshader.glsl`
```diff
uniform float time;
+uniform vec3 light;

varying float noise;
+varying vec3 positionVec;
+varying vec3 normalVec;

void main() {
+  vec3 normal = normalize(normalVec);
+  vec3 lightDir = normalize(light - positionVec);
+  float diffuse = max(0.0, dot(lightDir, normal));

  vec3 color = mix(vec3(0.0, 0.25, 0.0), vec3(0.0, 1.0, 0.0), noise);
+  vec3 finalColor = diffuse * color;

-  gl_FragColor = vec4(color, 1.0);
+  gl_FragColor = vec4(finalColor, 1.0);
}
```

Det er ikke s친 lett 친 se, men du skal n친 ha en sterkere gr칮nnfarge i midten av terrenget ditt. Vi kan forsterke effekten ved 친 si at alt som ikke f친r minst `0.5` av lyset skal v칝re helt m칮rkt:

`fragmentshader.glsl`:
```diff
void main() {
  vec3 normal = normalize(normalVec);
  vec3 lightDir = normalize(light - positionVec);
  float diffuse = max(0.0, dot(lightDir, normal));

  vec3 color = mix(vec3(0.0, 0.25, 0.0), vec3(0.0, 1.0, 0.0), noise);
-  vec3 finalColor = diffuse * color;
+  vec3 finalColor = (max(0.5, diffuse) - 0.5) * color;

  gl_FragColor = vec4(finalColor, 1.0);
}
```

Vi kan ogs친 si at lyset der det treffer skal v칝re litt sterkere:

```diff
-  vec3 finalColor = (max(0.5, diffuse) - 0.5) * color;
+  vec3 finalColor = (max(0.5, diffuse) - 0.5) * 2.0 * color;
```

N친 har vi en ganske kul lommelykt effekt, men den er litt kjedelig 친 se p친. Vi kan lage oss en liten kule som fungerer som lyskilde!

`index.js`
```diff
+const orbVertexShaderCode = glsl(
+  fs.readFileSync(`${__dirname}/orb-vertexshader.glsl`, "utf8")
+);
+const orbFragmentShaderCode = glsl(
+  fs.readFileSync(`${__dirname}/orb-fragmentshader.glsl`, "utf8")
+);

// ...

- let plane;
+let plane, orb;

// ...

initCamera();
initRenderer();
initDatGui();
initPlane();
+initOrb();

// ...

+function initOrb() {
+  let geometry = new THREE.SphereBufferGeometry(0.1, 8, 8);
+  let material = new THREE.ShaderMaterial({
+    vertexShader: orbVertexShaderCode,
+    fragmentShader: orbFragmentShaderCode,
+    uniforms
+  });
+
+  orb = new THREE.Mesh(geometry, material);
+  orb.position.set(0, 0, 2);
+  scene.add(orb);
+}
```

(Disse to shaderne er identiske med de standardshaderne dere fikk utdelt)

`orb-fragmentshader.glsl`:
```c
void main() {
  vec3 color = vec3(1.0, 1.0, 1.0);
  float alpha = 1.0;
  gl_FragColor = vec4(color, alpha);
}
```

`orb-vertexshader.glsl`:
```c
void main() {
  vec4 modelSpaceCoordinates = vec4(position, 1.0);
  vec4 worldSpaceCoordinates = modelViewMatrix * modelSpaceCoordinates;
  vec4 screenSpaceCoordinate = projectionMatrix * worldSpaceCoordinates;

  gl_Position = screenSpaceCoordinate;
}
```

Det vi har laga n친 er en liten kule, bare `0.1` i diameter (vi trenger ikke mer, siden scena v친r er ganske liten) med sine egne shadere (som vi kommer til 친 bruke senere) som har samme posisjon som lyskilden v친r.

Det du kanskje vil se etterhvert er at lyskilden v친r kolliderer med noen av 친sene vi har laga (spesielt hvis du har multiplisert noise-verdien og f친tt h칮yere 친ser). For 친 unng친 dette, og lage en ganske kul effekt, kan vi flytte b친de lyset og kula v친r opp like mye som vi flytter terrenget. Med andre ord, vi bruker noise-verdien til 친 flytte p친 lyset.

## Del 6: Moving lights.

Det f칮rste vi trenger n친 er 친 vite hvor mye lyset skal flytte seg, for 친 gj칮re det bruker vi noise-funksjonen v친r. S친 sender vi den verdien videre til fragment shaderen gjennom en varying.

`vertexshader.glsl`:
```diff
uniform float time;
+uniform vec3 light;

varying vec3 positionVec;
varying vec3 normalVec;
varying float noise;
+varying float lightNoise;

void main() {
  // ...

  positionVec = position;
  normalVec = normal;
  noise = n;
+  lightNoise = max(0.0, noiseFunction(time, vec3(light.xy, 0)));

  // ...
}
```

En ting det er verdt 친 merke seg her er at vi ikke bruker hele posisjonen til lyset n친r vi henter ut noise-verdien. Dette er for at vi skal bruke samme Z-komponent som terrenget (0) og ikke den verdien vi har p친 lyset (2).

`fragmentshader.glsl`:
```diff
varying float noise;
varying vec3 positionVec;
varying vec3 normalVec;
+varying float lightNoise;

void main() {
  vec3 normal = normalize(normalVec);
+  vec3 lightPos = vec3(light.xy, light.z + lightNoise);
-  vec3 lightDir = normalize(light - positionVec);
+  vec3 lightDir = normalize(lightPos - positionVec);
  float diffuse = max(0.0, dot(lightDir, normal));
  // ...
}
```

Legg merke til at vi flytter lyset langs Z-aksen, akkurat som vi gj칮r med vertex posisjonen i vertex-shaderen.

S친 m친 vi gj칮re noe tilsvarende med lys-kula v친r. Da trenger vi 친 gjenbruke noise funksjonen vi har i `vertexshader.glsl`. For 친 gj칮re det kan vi lage v친r egen `glslify`-modul. Det gj칮r vi enkelt ved 친 putte funksjonen i en egen fil og legge p친 en export-pragma:

`noise.glsl`:
```c
#pragma glslify: snoise3 = require(glsl-noise/simplex/3d);

float noiseFunction(float t, vec3 position) {
  vec3 offsetPosition = vec3(position.x, position.y + t, position.z);
  float n1 = snoise3(0.25 * offsetPosition);
  float n2 = snoise3(0.1 * offsetPosition);
  float n = n1 + n2;

  return n * 2.5;
}

#pragma glslify: export(noiseFunction)
```

S친 kan vi erstatte noise-funksjonen i `vertexshader.glsl` med den nye modulen slik:

`vertexshader.glsl`:
```diff
+#pragma glslify: noiseFunction = require(./noise.glsl);

// ...

-float noiseFunction(float t, vec3 position) {
-  vec3 offsetPosition = vec3(position.x, position.y + t, position.z);
-  float n1 = snoise3(0.25 * offsetPosition);
-  float n2 = snoise3(0.1 * offsetPosition);
-  float n = n1  + n2;
-
-  return n * 2.5;
-}
```

Da kan vi ogs친 bruke noise-funksjonen i `orb-vertexshader.glsl`:

`orb-vertexshader.glsl`
```diff
+#pragma glslify: noiseFunction = require(./noise.glsl);

+uniform float time;
+uniform vec3 light;

void main() {
+  float n = max(0.0, noiseFunction(time, vec3(light.xy, 0)));

-  vec4 modelSpaceCoordinates = vec4(position, 1.0);
+  vec4 modelSpaceCoordinates = vec4(position.xy, position.z + n, 1.0);
  vec4 worldSpaceCoordinates = modelViewMatrix * modelSpaceCoordinates;
  vec4 screenSpaceCoordinate = projectionMatrix * worldSpaceCoordinates;

  gl_Position = screenSpaceCoordinate;
}
```

N친 har vi et lys som flytter seg i takt med terrenget!

>游눠 Du kan fikle med parameterne til lyset og kula, s친nn som feks hvor h칮yt over bakken utgangsposisjonen skal v칝re eller hvor langt lyset lyser eller hvor sterkt lyset lyser.

Vi kan ogs친 flytte lyset litt fra side til side, for 친 f친 en litt mer interessant effekt. Det kan vi gj칮re ved 친 endre utgangsposisjonen til b친de lyset og kula i render-funksjonen v친r. Vi bruker sinus av tiden til 친 f친 en fin oscillering p친 banen:

`index.js`
```diff
function render() {
  requestAnimationFrame(render);
  // time in s
  let time = (Date.now() - t0) * 0.001;
  uniforms.time.value = time;

+  let displacement = 2.5 * Math.sin(0.5 * time);
+  uniforms.light.value = new THREE.Vector3(displacement, 0, 2);
+  orb.position.set(displacement, 0, 2);

  renderer.render(scene, camera);
}
```

Gratulerer, du har n친 laga et procedurally generated terreng med en liten lysende kule som er p친 en evig reise i m칮rket! Passer bra til 친rstida.

Det er mange g칮yale parametre 친 skru p친 i denne visualiseringen, s친 her er det bare 친 v칝re kreativ.
